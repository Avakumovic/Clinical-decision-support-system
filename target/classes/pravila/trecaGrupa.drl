package ljubisa

import ljubisa.model.*;
import java.util.Set
import java.util.List
import java.time.LocalDate
global Bolest prehlada;
global Bolest groznica;
global Bolest upalaKrajnika;
global Bolest sinusnaInfekcija;
global Bolest hronicnaBubreznaBolest;
global Bolest akutnaBubreznaBolest;
global Bolest hipertenzija;
global Bolest dijabetes;

rule "bolovanje od hipertenzije vishe od 6 meseci"
agenda-group "treca"
salience 330
	when
		$pre : Pregled($pac : pacijent)
		$pcc : Pacijent(this == $pac, $ter : terapije) from $pac
		$lis : List(size>0) from collect ($trr : Terapija(bolest.naziv == "hipertenzija",
		$trr.getDatum().isBefore(LocalDate.now().minusMonths(6)))from $ter) 
 	then
		$pre.getSpecificniSimptomi().add(SpecificniSimptomi.BOLUJE_OD_HIPERTENZIJE_DUZE_OD_6_MESECI);
end

rule "boluje od dijabetesa"
agenda-group "treca"
salience 330
	when
		$pre : Pregled($pac : pacijent )
		$pcc : Pacijent(this == $pac, $ter : terapije) from $pac
		$lis : List(size > 0) from collect ($pt : Terapija((bolest.naziv =="dijabetes" ))from $ter) 
 	then
		$pre.getSpecificniSimptomi().add(SpecificniSimptomi.BOLUJE_OD_DIJABETESA);
end

rule "poslednjih 14 dana dijagnostikovana boles koja kao simptom ima povecanu telestnu temperaturu"
agenda-group "treca"
salience 330
	when
		$pre : Pregled($pac:pacijent)
		$pcc : Pacijent(this==$pac, $ter : terapije) from $pac
		$lis: List (size > 0) from collect ($pt : Terapija((uZadnjih14Dana() &&
		((bolest.sviSimptomi contains (SviSimptomi.TEMPERATURA_IZMEDJU_40_I_41)) ||
		(bolest.sviSimptomi contains (SviSimptomi.TEMPERATURA_VECA_OD_38)))))from $ter) 
 	then
		$pre.getSpecificniSimptomi().add(SpecificniSimptomi.POVISENA_TEMPERATURA_U_POSLEDNJIH_14_DANA);
end

rule "poslednjih 21 primao antibiotike"
agenda-group "treca"
salience 330
	when
		$pre : Pregled($pac:pacijent)
		$pcc : Pacijent(this==$pac, $ter : terapije) from $pac
		$tr2 : Terapija($lekovi : lekovi, uZadnjih21Dana())from $ter
		$l:List(size>0) from collect (Lek(jeProbiotic()) from $lekovi) 
 	then
		$pre.getSpecificniSimptomi().add(SpecificniSimptomi.PREPISANI_ANTIBIOTICI_U_POSLEDNIH_21_DAN);	
end


rule "hronicnaBubreznaBolest moze biti"
agenda-group "treca"
salience 300
    when
        $pre : Pregled($svi : sviSimptomi, $pot : potencijalneBolesti, $spec : specificniSimptomi)  and
		$stt:Set() from collect($sss:SviSimptomi(
			this==SviSimptomi.UMOR ||
			this==SviSimptomi.NOCTURIA ||
			this==SviSimptomi.OTOCI_NOGU_I_ZGLOBOVA ||
			this==SviSimptomi.GUSENJE ||
			this==SviSimptomi.BOL_U_GRUDIMA
		) from $svi) and
		eval($stt.size() > 1)
		$sSpec : Set() from collect ($spSim : SpecificniSimptomi( 
		 	this==SpecificniSimptomi.BOLUJE_OD_DIJABETESA ||
		 	this==SpecificniSimptomi.BOLUJE_OD_HIPERTENZIJE_DUZE_OD_6_MESECI)
		from $spec) and
		eval($sSpec.size() >= 1) and
		$pob : PotencijalnaBolest(nazivBolesti=="hronicnaBubreznaBolest") from $pot
    then
        $pob.setSansa(($stt.size() + 2*$sSpec.size())/9.0);
end

rule "akutnaBubreznaBolest moze biti"
agenda-group "treca"
salience 300
    when
	    $pre : Pregled($svi : sviSimptomi, $pot : potencijalneBolesti, $spec : specificniSimptomi)  and
		$stt : Set() from collect($smm : SviSimptomi(
			this==SviSimptomi.UMOR ||
			this==SviSimptomi.GUSENJE ||
			this==SviSimptomi.OTOCI_NOGU_I_ZGLOBOVA ||
			this==SviSimptomi.DIJAREJA
		 ) from $svi) and
		eval($stt.size()>1)
		$sSpec : Set() from collect ($spSim : SpecificniSimptomi( 
		 	this== SpecificniSimptomi.OPORAVLJA_SE_OD_OPERACIJE ||
		 	this== SpecificniSimptomi.POVISENA_TEMPERATURA_U_POSLEDNJIH_14_DANA ||
		 	this== SpecificniSimptomi.PREPISANI_ANTIBIOTICI_U_POSLEDNIH_21_DAN  
		  )
		from $spec) and
		eval($sSpec.size() >= 1) and
		$pob : PotencijalnaBolest(nazivBolesti=="akutnaBubreznaBolest") from $pot
		 
    then
        $pob.setSansa(($stt.size() + 2*$sSpec.size())/10.0);
end

rule "nadji najbolu mogucnost za trecu grupu"
agenda-group "treca"
salience 200
	when
		$pre : Pregled($pot : potencijalneBolesti)
		$dou : Double(doubleValue >= 0.0) from accumulate(
		$mb : PotencijalnaBolest($san : sansa) from $pot,max($san))
	then
		$pre.setNajboljaProcena($dou);
end

rule "da li je hronicnaBubreznaBolest"
agenda-group "treca"
salience 109
enabled false
	when
		$pre : Pregled($pot : potencijalneBolesti, $naj : najboljaProcena)
		$lis : List(size > 0) from collect ($pbb : PotencijalnaBolest(sansa == $naj && nazivBolesti == hronicnaBubreznaBolest.getNaziv()) from $pot)
	then
		Terapija ter = new Terapija(hronicnaBubreznaBolest,null,null,null);
		$pre.getPacijent().getTerapije().add(ter);
		int temp = $pre.getPacijent().getTerapije().size();
end

rule "da li je akutnaBubreznaBolest"
agenda-group "treca"
enabled false
salience 110
	when
		$pre : Pregled($pot : potencijalneBolesti, $naj : najboljaProcena)
		$lis : List(size > 0) from collect($pbb : PotencijalnaBolest(sansa == $naj && nazivBolesti == akutnaBubreznaBolest.getNaziv()) from $pot)
	then
		Terapija ter = new Terapija(akutnaBubreznaBolest,null,null,null);
		$pre.getPacijent().getTerapije().add(ter);
		int temp = $pre.getPacijent().getTerapije().size();
end